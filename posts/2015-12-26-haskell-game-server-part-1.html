<?xml version="1.0" encoding="UTF-8" ?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
        <title>mojobojo.com - Haskell Game Server - Part 1</title>
        <link rel="stylesheet" type="text/css" href="../css/default.css" />
        <link rel="stylesheet" href="../css/highlight.css" />
        <script src="../js/highlight.pack.js"></script>
        <script>hljs.initHighlightingOnLoad();</script>
        <script type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-1531113-1']);
            _gaq.push(['_trackPageview']);
            (function() {
            var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
            ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
            var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
      </script>
    </head>
    <body>
        <div id="header">
            <div id="logo">
                <a href="../">mojobojo.com</a>
            </div>
            <div id="navigation">
                <a href="../archive.html">Archive</a>
                <a href="https://twitter.com/mojobojo">@mojobojo</a>
                <a href="https://github.com/boj">boj@GitHub</a>
            </div>
        </div>

        <div id="content">
            <h1>Haskell Game Server - Part 1</h1>
<div class="info">
    Posted on December 26, 2015
    
</div>

<p>This post describes the project <a href="http://armoredbits.com/">Armored Bits</a> which <a href="https://twitter.com/mojobojo">myself</a> and my buddy <a href="https://twitter.com/cerbermus">cerbemus</a> have been working on for over half a year in our spare time. The goal is to build a realtime, network team based, player programmable 3d mech simulator. As the title suggests the core game server is being written in Haskell, a choice I made after having been inspired by a <a href="https://www.youtube.com/watch?v=1PhArSujR_A">QuakeCon talk</a> with John Carmack, the linked video a part of that talk specifically about functional programming and games. There is also a client viewer written in Unity3d and our first SDK written in JavaScript both being tackled by cerbermus.</p>
<p>Not including a failed attempt several years ago I have only been using Haskell for the last 9 months and am by all accounts an amatuer with the language. It has proven quite enjoyable to use, however, especially as of late now that I’ve gotten rid of some of my imperative programming notions and understand more functional programming concepts better.</p>
<p>My goal with this post is to show a concrete non-web example of a project that is being built with Haskell. People in the <a href="https://www.reddit.com/r/haskell/">community</a> have pointed out that there are a plethora of beginner level tutorials and high level you-need-a-PhD-to-read-it articles, but not much in the way of meat and potatoes concrete project guides. This is my humble attempt to share back to the community I’ve learned so much from over the last several months.</p>
<h2 id="defining-the-game-server">Defining the game server</h2>
<p>First and foremost we need to make a list of our requirements.</p>
<ul>
<li>TCP networking which exposes a port for external 3d clients and a port for internal (local) SDK connections.</li>
<li>A way for the server and clients to communicate via some kind of messaging protocol.</li>
<li>A way to track client connections and link them to their corresponding game entities.</li>
<li>Scene management and the game loop.</li>
<li>A linear algebra library to manipulate scene objects.</li>
<li>A configuration phase and configuration sanity checker.</li>
<li>A query interface for SDK clients to configure their mech, get information about it, and put values to manipulate its behavior.</li>
</ul>
<p>In today’s post I will address the TCP networking and messaging protocol.</p>
<h2 id="networking">Networking</h2>
<p>We decided to strictly use TCP because this game doesn’t require human input, nor all the magical UDP networking and iterpolation/extrapolation tricks of a first person shooter. The 3d client simply processes messages and displays the scene based on that data, while the SDK clients run on the same network as the game server.</p>
<p>The primary <code>Main.hs</code> code is as follows:</p>
<pre><code class="haskell">
module Main where

import           Control.Concurrent    (forkIO)
import           Data.Yaml             (ParseException, decodeFileEither)
import           Network.Socket

import           Data.Configurations
import           Game.Server

internalPort :: PortNumber
internalPort = 4000

externalPort :: PortNumber
externalPort = 5000

maxInternalConns :: Int
maxInternalConns = 10

maxExternalConns :: Int
maxExternalConns = 10

maxPlayers :: Int
maxPlayers = 10

acceptClients :: (Server -> Socket -> IO ()) -> Server -> Socket -> IO ()
acceptClients handler server sock = do
  (next, _) <- accept sock
  setSocketOption next NoDelay 1 -- disable nagle
  _ <- handler server next
  acceptClients handler server sock

startListener :: PortNumber -> Int -> (Server -> Socket -> IO ()) -> Server -> IO ()
startListener port maxClients handler server = do
  sock <- socket AF_INET Stream defaultProtocol
  setSocketOption sock ReuseAddr 1
  bindSocket sock $ SockAddrInet port iNADDR_ANY
  listen sock maxClients
  acceptClients handler server sock

main :: IO ()
main = withSocketsDo $ do
  -- load configurations
  configData <- decodeFileEither "./data/configurations.yml" :: IO (Either ParseException Configurations)
  case configData of
    Left err ->
      print err
    Right configs -> do
      server <- newServer configs maxPlayers
      -- external client connections
      _ <- forkIO $ startListener externalPort maxExternalConns addExternalClient server
      -- internal client connections
      _ <- forkIO $ startListener internalPort maxInternalConns addClient server
      -- start server
      runWorld server
</code></pre>
<p>We’ll break this down into simple to understand pieces.</p>
<p>The first section simply loads our <code>configurations.yml</code> which contains a large amount of mech configuration data (I’m in the process of moving server settings out to their own configuration file as well). It simply loads the file and prints out why it failed with the <code>Left</code> evaluation of the <code>Either</code> type which is returned.</p>
<p>We apparently need <code>withSocketsDo</code> if this code is to ever run on a Windows machine.</p>
<pre><code class="haskell">
main :: IO ()
main = withSocketsDo $ do
  -- load configurations
  configData <- decodeFileEither "./data/configurations.yml" :: IO (Either ParseException Configurations)
  case configData of
    Left err ->
      print err
</code></pre>
<p>Upon successful YAML parsing the <code>Either</code>’s <code>Right</code> is evaluated and creates a new <code>Server</code> (to be defined later). It then runs the network listeners for the internal and external clients in their owns threads, and finally runs the game loop in the main thread.</p>
<pre><code class="haskell">
    Right configs -> do
      server <- newServer configs maxPlayers
      -- external client connections
      _ <- forkIO $ startListener externalPort maxExternalConns addExternalClient server
      -- internal client connections
      _ <- forkIO $ startListener internalPort maxInternalConns addClient server
      -- start server
      runWorld server
</code></pre>
<p>The <code>startListener</code> function is designed to take a handler function with the signature <code>Server -&gt; Socket -&gt; IO()</code> and listen on the designated port for new client connections. These new connections then get passed off to the <code>acceptClients</code> function.</p>
<pre><code class="haskell">
startListener :: PortNumber -> Int -> (Server -> Socket -> IO ()) -> Server -> IO ()
startListener port maxClients handler server = do
  sock <- socket AF_INET Stream defaultProtocol
  setSocketOption sock ReuseAddr 1
  bindSocket sock $ SockAddrInet port iNADDR_ANY
  listen sock maxClients
  acceptClients handler server sock
</code></pre>
<p>New socket connections use the socket option <code>NoDelay 1</code> in order to disable the Nagle Algorithm and decrease network latency. The handler then takes the newly connected socket and hands it off to the <code>Server</code> via the handler so it can map a connection to a <code>Client</code> entity, then the listener calls itself again and waits for the next incoming connection.</p>
<pre><code class="haskell">
acceptClients :: (Server -> Socket -> IO ()) -> Server -> Socket -> IO ()
acceptClients handler server sock = do
  (next, _) <- accept sock
  setSocketOption next NoDelay 1 -- disable nagle
  _ <- handler server next
  acceptClients handler server sock
</code></pre>
<h2 id="communication">Communication</h2>
<p>The next step is to determine a communication strategy. We’ve used <a href="https://developers.google.com/protocol-buffers/?hl=en">Google’s Protocol Buffers</a> before on other projects and they seem like a reasonable way to efficiently pass streaming data back and forth over the wire. The specific Haskell library I am using is <a href="https://hackage.haskell.org/package/protobuf">protobuf</a>. Unfortunately it is not built to auto generate code from <code>.proto</code> files (yet, apparently) which does lead to headaches when you miss a field or put the wrong type in the <code>.proto</code> file your buddy uses, resulting in him scratching his head wondering why things don’t work. The library itself has been quite the pleasure to use though.</p>
<p>We handle message encoding/decoding with a couple simple functions.</p>
<pre><code class="haskell">
{-# LANGUAGE DataKinds     #-}
{-# LANGUAGE DeriveGeneric #-}

module Game.Messages where

import qualified Data.Binary           as DB
import qualified Data.ByteString.Char8 as BS
import qualified Data.ByteString.Lazy  as BL
import           Data.Int
import           Data.ProtocolBuffers
import           Data.Serialize
import           Data.Word

class GameMessage m where
  getMessageId :: m -> Word8

messageIn :: (GameMessage m, Decode m) => BS.ByteString -> Either String m
messageIn =
  runGet decodeMessage

messageOut :: (GameMessage m, Encode m) => m -> BS.ByteString
messageOut =
  runPut . encodeMessage

messageOutWithIdAndLength :: (GameMessage m, Encode m) => m -> BS.ByteString
messageOutWithIdAndLength msg =
  BS.concat [mid1, len, out]
  where
    mid1 = BL.toStrict $ DB.encode (getMessageId msg)
    out  = messageOut msg
    len  = BL.toStrict $ DB.encode (fromIntegral (BS.length out) :: Int16)
</code></pre>
<p>We’ve chosen to make our messages 3 bytes at the very least. The first byte denotes the message id, the second two bytes the size of the body of the message, and the remaining bytes the actual body if applicable. We are unfortunately wasting 2 bytes in the case of a message that has no body, however, it means we don’t have to pre-check what kind of message is being passed and avoid writing specific code to handle those exceptions.</p>
<p>The <code>messageOutWithIdAndLength</code> uses our handy <code>GameMessage</code> class to pull the predefined message id and <code>concat</code> it together with the message’s length and body.</p>
<p>An example message looks something like this:</p>
<pre><code class="haskell">
{-# LANGUAGE DataKinds     #-}
{-# LANGUAGE DeriveGeneric #-}

module Game.Messages.ClientActionLoginRequest where

import           Data.ProtocolBuffers
import           GHC.Generics          (Generic)

import           Game.Messages

data ClientActionLoginRequest
  = ClientActionLoginRequest {
      username :: Required 1 (Value String)
    , password :: Required 2 (Value String)
    }
  deriving (Generic, Show)

instance Encode ClientActionLoginRequest
instance Decode ClientActionLoginRequest
instance GameMessage ClientActionLoginRequest where
  getMessageId _ = 200
</code></pre>
<p>And the relevant sections of code from <code>Server.hs</code> to send and receive messages. I am using <a href="https://hackage.haskell.org/package/io-streams">io-streams</a> which is a fantastically simple to use I/O library for processing streams.</p>
<p>The <code>runClient</code> function runs in its own thread and handles the incoming data from the associated client connection. <code>UserId</code> is a unique identifier which associates the socket with a <code>Client</code> and their <code>Player</code> data.</p>
<pre><code class="haskell">
-- receive messages
runClient :: Server -> Socket -> UserId -> IO ()
runClient server sock uid = do
  (inS, _) <- Streams.socketToStreams sock        -- utilize the incoming half of the socket
  mid <- Streams.readExactly 1 inS                -- parse out the message id
  let sid = fromIntegral $ runGet getWord8 (BL.fromStrict mid) -- turn the byte into a Word8
  lth <- Streams.readExactly 2 inS                -- parse out the message length
  when (msgSize lth <= maxMsgSize) $ do           -- check against a max message size to stop abuse
    msg <- Streams.readExactly (msgSize lth) inS  -- parse out the message body if applicable
    handleMessage server uid sid msg              -- handle the message
  runClient server sock uid                       -- loop and wait for the next message
  where
    msgSize lth = (fromIntegral $ runGet getWord16be (BL.fromStrict lth)) :: Int

-- send message
sendMessage :: (GameMessage m, Encode m) => m -> Socket -> IO ()
sendMessage msg sock = do
  (_, outS) <- Streams.socketToStreams sock
  handle (\(SomeException e) -> traceIO $ show e ) $
    Streams.write (Just $ messageOutWithIdAndLength msg) outS
</code></pre>
<h2 id="conclusion">Conclusion</h2>
<p>Building a networked game server takes an incredible amount of work and we’ve only begun to scratch the surface of all the components which bring it together. Today I touched on some basic networking and communication techniques we are using such as low level <code>Network.Socket</code>’s wrapped in <code>System.IO.Streams</code>, and message passing techniques with <code>Data.ProtocolBuffers</code>. In my next post I will discuss how clients interact with the game, how the game world is managed, and how our <code>Actor</code> class helps us manage scene objects.</p>

<div id="disqus_thread"></div>
<script>
this.page.url = "http://mojobojo.com";
this.page.identifier = "Haskell Game Server - Part 1";

(function() { // DON'T EDIT BELOW THIS LINE
var d = document, s = d.createElement('script');

s.src = '//mojobojo.disqus.com/embed.js';

s.setAttribute('data-timestamp', +new Date());
(d.head || d.body).appendChild(s);
})();
</script>
<noscript>Please enable JavaScript to view the <a href="https://disqus.com/?ref_noscript" rel="nofollow">comments powered by Disqus.</a></noscript>

        </div>
        <div id="footer">
            Site proudly generated by
            <a href="http://jaspervdj.be/hakyll">Hakyll</a>
        </div>
    </body>
</html>
